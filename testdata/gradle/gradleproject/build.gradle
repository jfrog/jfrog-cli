apply plugin: 'groovy'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

// Gradle 8+ blocks http:// repositories unless allowInsecureProtocol=true is set.
// The JFrog Gradle integration (traditional extractor flow) may add an Artifactory resolver repo using http://localhost in tests.
// This block is safe across Gradle versions because it checks for the property before setting it.
allprojects {
    repositories.all { repo ->
        try {
            def urlStr = repo.respondsTo('getUrl') && repo.url != null ? repo.url.toString() : ""
            if (urlStr.startsWith("http://") && repo.hasProperty("allowInsecureProtocol")) {
                repo.allowInsecureProtocol = true
            }
        } catch (Throwable ignored) {
            // Best-effort; ignore repositories that don't support URL/allowInsecureProtocol.
        }
    }
}

dependencies {
    implementation "junit:junit:4.7"
}

version = 1.0
task initProject(description: 'Initialize project directory structure.') {
    doLast {
        // Default package to be created in each src dir.
        def defaultPackage = 'org/jfrog/minimal-project'

        ['java', 'groovy', 'resources'].each {
            // convention.sourceSets contains the directory structure
            // for our Groovy project. So we use this structure
            // and make a directory for each node.
            sourceSets*."${it}".srcDirs*.each { dir ->
                def newDir = new File(dir, defaultPackage)
                logger.info "Creating directory $newDir"  // gradle -i shows this message.
                newDir.mkdirs()  // Create dir.
            }
        }
    }
}