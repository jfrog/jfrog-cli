buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.+')
    }
}
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'com.jfrog.artifactory'
version = 1.0
task initProject(description: 'Initialize project directory structure.') {
    doLast {
        // Default package to be created in each src dir.
        def defaultPackage = 'org/jfrog/minimal-project'

        ['java', 'groovy', 'resources'].each {
            // convention.sourceSets contains the directory structure
            // for our Groovy project. So we use this structure
            // and make a directory for each node.
            sourceSets*."${it}".srcDirs*.each { dir ->
                def newDir = new File(dir, defaultPackage)
                logger.info "Creating directory $newDir"  // gradle -i shows this message.
                newDir.mkdirs()  // Create dir.
            }
        }
    }
}

artifactory {
    contextUrl = "${URL}"
    publish {
        repository {
            repoKey = "${REPO1}" // The Artifactory repository key to publish to
            username = "${USERNAME}" // The publisher user name
            password = "${PASSWORD}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publishConfigs('archives', 'published')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team' : 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}